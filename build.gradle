apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.rainbowpunch'
archivesBaseName = "jetedge"
version = '0.10.2-SNAPSHOT'

description = """Jetedge"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
     maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'ch.qos.logback', name: 'logback-classic', version:'1.2.3'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.25'
    testCompile group: 'org.projectlombok', name: 'lombok', version:'1.16.16'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version:'1.3'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version:'2.9.0'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from sourceSets.main.allSource
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Jetedge'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'This is a Java library for quickly and easily creating random data based off of YOUR POJOs'
                url 'http://www.rainbowpunch.com'

                scm {
                    connection 'scm:git:git://github.com/Bekreth/jetedge.git'
                    developerConnection 'scm:git:ssh://github.com:Bekreth/jetedge.git'
                    url 'https://github.com/Bekreth/jetedge'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'bekerth'
                        name 'Christopher Hughes'
                        email 'christopher.hughes@rainbowpunch.com'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}
